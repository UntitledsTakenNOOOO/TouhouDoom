ACTOR MegaFlare : CustomInventory 28120
{
//$Category "Touhou Spellcards"
  +COUNTITEM
  +INVENTORY.INVBAR
  +FLOATBOB
  Inventory.MaxAmount 1
  Inventory.Icon "MGFLA0"
  Inventory.PickupMessage "Explosion Sign \"Mega Flare\""
  Tag "Explosion Sign \"Mega Flare\""
  Inventory.PickupSound "touhou/bonus1"
  States
  {
  Spawn:
	TNT1 A 0
	TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(610,2),"Bye")
	MGFL B -1
	Stop
  Bye:
	TNT1 A 0 A_ChangeFlag("COUNTITEM", FALSE)
	TNT1 A 0 A_SpawnItemEx("SoulSphere",0,0,0,0,0,0,0,0,0)
    Stop
  Use:
	TNT1 A 0 A_JumpIfInventory("SpellAmplifierPowerup",1,"Use2Continue")
	TNT1 A 0 A_JumpIfHealthLower(25,"Use2")
  Use1:
    TNT1 A 0 ACS_ExecuteAlways(612,0,19)
	TNT1 A 0 ACS_Execute(605,0)
	TNT1 A 0 A_PlaySound("touhou/spellcard",0,1.0,0,ATTN_NONE)
	TNT1 A 0 A_PlaySound("touhou/utsuho/alarm",0,1.0,0,ATTN_NONE)
	TNT1 A 0 A_GiveInventory("BeaconInvuln",1)
	TNT1 A 0 A_FireCustomMissile("MegaFlareAttack",0,0,0)
    Stop
  Use2:
	TNT1 A 0 A_JumpIfInventory("Power",400,"Use2Continue")
	Goto Use1
  Use2Continue:
    TNT1 A 0 ACS_ExecuteAlways(612,0,20)
	TNT1 A 0 ACS_Execute(605,0)
	TNT1 A 0 A_PlaySound("touhou/spellcard",0,1.0,0,ATTN_NONE)
	TNT1 A 0 A_PlaySound("touhou/utsuho/alarm",0,1.0,0,ATTN_NONE)
	TNT1 A 0 A_GiveInventory("BeaconInvuln",1)
	TNT1 A 0 A_FireCustomMissile("GigaFlareAttack3",30,0,-192)
	TNT1 A 0 A_FireCustomMissile("GigaFlareAttack3",-30,0,192)
	TNT1 A 0 A_FireCustomMissile("GigaFlareAttack2",15,0,-96)
	TNT1 A 0 A_FireCustomMissile("GigaFlareAttack2",-15,0,96)
	TNT1 A 0 A_FireCustomMissile("GigaFlareAttack",0,0,0)
    Stop
  }
}
ACTOR MegaFlareAttack
{
  Radius 192
  Height 256
  Health 0x7FFFFFFF
  DamageFactor 0.0
  RadiusDamageFactor 0.0
  Species "Player"
  DamageType "PlayerSpellcard"
  Speed 6
  Damage (0)
  Scale 2.0
  Projectile
  -SOLID
  +THRUSPECIES
  +SHOOTABLE
  +NOBLOOD
  +DONTSPLASH
  +NOCLIP
  -NOBLOCKMAP
  +FORCEXYBILLBOARD
  +SEEKERMISSILE
  +FORCERADIUSDMG
  +PAINLESS
  +FLOORHUGGER
  +RANDOMIZE
  RenderStyle Add
  SeeSound "touhou/nuefire"
  DeathSound ""
  States
  {
  Spawn:
	USUN A 0
  LoopState:
	USUN A 0 Bright A_GiveInventory("EnemyCounter1",1)
	USUN A 0 Bright A_JumpIfInventory("EnemyCounter1",175,"Crash")
	USUN A 0 Bright A_Explode(10,512,0,0,384)
	USUN A 1 Bright A_SpawnItemEx("UtsuhoNuclearFusionSunTrail")
	Loop
  Crash:
  Death:
  XDeath:
	USUN AAAAAAAAAA 4 Bright A_FadeOut
	Stop
  }
}
ACTOR GigaFlareAttack : MegaFlareAttack
{
  Speed 8
  States
  {
  Spawn:
	USUN A 0
  LoopState:
	USUN A 0 Bright A_GiveInventory("EnemyCounter1",1)
	USUN A 0 Bright A_JumpIfInventory("EnemyCounter1",175,"Crash")
	USUN A 0 Bright A_Explode(6,512,0,0,384)
	USUN A 1 Bright A_SpawnItemEx("UtsuhoNuclearFusionSunTrail")
	Loop
  Crash:
  Death:
  XDeath:
	USUN AAAAAAAAAA 4 Bright A_FadeOut
	Stop
  }
}
ACTOR GigaFlareAttack2 : GigaFlareAttack
{
	Speed 6
}
ACTOR GigaFlareAttack3 : GigaFlareAttack
{
	Speed 4
}
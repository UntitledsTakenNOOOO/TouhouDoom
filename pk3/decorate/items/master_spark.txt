ACTOR MasterSpark : CustomInventory 28113
{
//$Category "Touhou Spellcards"
  +COUNTITEM
  +INVENTORY.INVBAR
  +FLOATBOB
  Inventory.MaxAmount 2
  Inventory.Icon "MASPA0"
  Inventory.PickupMessage "Love Sign \"Master Spark\""
  Tag "Love Sign \"Master Spark\""
  Inventory.PickupSound "touhou/bonus1"
  States
  {
  Spawn:
	TNT1 A 0
	TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(610,2),"Bye")
	MASP B -1
	Stop
  Bye:
	TNT1 A 0 A_ChangeFlag("COUNTITEM", FALSE)
	TNT1 A 0 A_SpawnItemEx("SoulSphere",0,0,0,0,0,0,0,0,0)
    Stop
  Use:
	TNT1 A 0 A_JumpIfInventory("SpellAmplifierPowerup",1,"Use2Continue")
	TNT1 A 0 A_JumpIfHealthLower(25,"Use2")
  Use1:
    TNT1 A 0 ACS_ExecuteAlways(610,0,3)
    TNT1 A 0 ACS_ExecuteAlways(612,0,3)
	TNT1 A 0 Radius_Quake(6,70,0,16,0)
	TNT1 A 0 A_PlaySound("touhou/spellcard",0,1.0,0,ATTN_NONE)
	TNT1 A 0 A_PlaySound("touhou/masterspark",0,1.0,0,ATTN_NONE)
	TNT1 A 0 A_GiveInventory("BeaconInvuln",1)
	TNT1 A 0 A_RailAttack(0,0,0,"none","none",0,0,"MasterSparkPuff",0,0,0,0,16.0,0,"MasterSparkBeam")
    Stop
  Use2:
	TNT1 A 0 A_JumpIfInventory("Power",400,"Use2Continue")
	Goto Use1
  Use2Continue:
    TNT1 A 0 ACS_ExecuteAlways(610,0,3)
    TNT1 A 0 ACS_ExecuteAlways(612,0,4)
	TNT1 A 0 Radius_Quake(9,100,0,24,0)
	TNT1 A 0 A_PlaySound("touhou/spellcard",0,1.0,0,ATTN_NONE)
	TNT1 A 0 A_PlaySound("touhou/masterspark",0,1.0,0,ATTN_NONE)
	TNT1 A 0 A_GiveInventory("BeaconInvuln",1)
	TNT1 A 0 A_RailAttack(0,0,0,"none","none",0,65535,"MasterSparkPuff",0,0,0,0,32.0,0,"MasterSparkBeam")
	TNT1 A 0 A_RailAttack(0,0,0,"none","none",0,65535,"MasterSparkPuff",0,0,0,0,32.0,0,"MasterSparkBeam")
	TNT1 A 0 A_RailAttack(0,0,0,"none","none",0,65535,"MasterSparkPuff",0,0,0,0,32.0,0,"MasterSparkBeam")
	TNT1 A 0 A_RailAttack(0,0,0,"none","none",0,0,"MasterSparkPuff",0,0,0,0,32.0,0,"MasterSparkBeam")
    Stop
  }
}
ACTOR MasterSparkPuff : BulletPuff
{
	DamageType "PlayerSpellcard"
}
ACTOR MasterSparkBeam
{
  Radius 64
  Height 128
  Health 0x7FFFFFFF
  Scale 8.0
  DamageFactor 0.0
  RadiusDamageFactor 0.0
  Species "Player"
  DamageType "PlayerSpellcard"
  Speed 0
  Damage (0)
  Projectile
  -SOLID
  +THRUSPECIES
  +SHOOTABLE
  +NOBLOOD
  +DONTSPLASH
  +NOCLIP
  -NOBLOCKMAP
  +FORCEXYBILLBOARD
  +SEEKERMISSILE
  +FORCERADIUSDMG
  +PUFFGETSOWNER
  +NODAMAGETHRUST
  +PAINLESS
  RenderStyle Add
  SeeSound "touhou/fire"
  DeathSound "touhou/hit2"
  States
  {
  Spawn:
	TNT1 A 0 Bright A_JumpIfInventory("EnemyCounter1",50,"Death")
	TNT1 A 0 Bright A_GiveInventory("EnemyCounter1",1)
	TNT1 A 0 Bright A_Explode(1,384,0,0,384) //Doesn't need to be high due to multiple hits at once
	TNT1 A 0 Bright A_Jump(255,"Red","Orange","Yellow","Green","Cyan","Blue","Violet","White")
  Red:
	RBL4 A 2 Bright
	Goto Spawn
  Orange:
	OBL4 A 2 Bright
	Goto Spawn
  Yellow:
	YBL4 A 2 Bright
	Goto Spawn
  Green:
	GBL4 A 2 Bright
	Goto Spawn
  Cyan:
	CBL4 A 2 Bright
	Goto Spawn
  Blue:
	BBL4 A 2 Bright
	Goto Spawn
  Violet:
	VBL4 A 2 Bright
	Goto Spawn
  White:
	WBL4 A 2 Bright
	Goto Spawn
  Death:
	TNT1 A 0 Bright A_Jump(255,"RedDeath","OrangeDeath","YellowDeath","GreenDeath","CyanDeath","BlueDeath","VioletDeath","WhiteDeath")
  RedDeath:
	RBL4 A 2 Bright A_FadeOut(0.04)
	Goto Death
  OrangeDeath:
	OBL4 A 2 Bright A_FadeOut(0.04)
	Goto Death
  YellowDeath:
	YBL4 A 2 Bright A_FadeOut(0.04)
	Goto Death
  GreenDeath:
	GBL4 A 2 Bright A_FadeOut(0.04)
	Goto Death
  CyanDeath:
	CBL4 A 2 Bright A_FadeOut(0.04)
	Goto Death
  BlueDeath:
	BBL4 A 2 Bright A_FadeOut(0.04)
	Goto Death
  VioletDeath:
	VBL4 A 2 Bright A_FadeOut(0.04)
	Goto Death
  WhiteDeath:
	WBL4 A 2 Bright A_FadeOut(0.04)
	Goto Death
  }
}
actor DanmakuDarkImp 3101
{
//$Category "Danmaku Demons"
    Health 120
    Radius 20
    Height 56
    Speed 7
    PainChance 200
    MONSTER 
    +FLOORCLIP
    SeeSound "imp/sight"
    PainSound "imp/pain"
    DeathSound "imp/death"
    ActiveSound "imp/active"
    MeleeSound "imp/melee"
    Obituary "%o was cursed by a dark imp."
    HitObituary "%o was touched by a dark imp."
    MissileType DanmakuDarkNormal
    MeleeDamage 3
	DamageFactor "DanmakuImp", 0
    States
    {
    Spawn:
        DRKV AB 10 A_Look
        Loop
    See:
        DRKV AABBCCDD 3 A_Chase
        Loop
    Melee:
        DRKV EF 8 A_FaceTarget
        DRKV G 6 A_ComboAttack
        Goto See
	Missile:
		TNT1 A 0 A_FaceTarget
		DRKV A 0 A_JumpIfInTargetInventory("EasyMode",1,"MissileEasy")
		DRKV A 0 A_JumpIfInTargetInventory("NormalMode",1,"MissileNormal")
		DRKV A 0 A_JumpIfInTargetInventory("HardMode",1,"MissileHard")
		DRKV A 0 A_JumpIfInTargetInventory("LunaticMode",1,"MissileLunatic")
		Goto MissileNormal
	MissileRepeatFire:
		DRKV EF 3 A_FaceTarget
		DRKV G 3 A_CustomMissile("DanmakuDarkFast",32,0,0)
		DRKV EF 3 A_FaceTarget
		DRKV G 3 A_CustomMissile("DanmakuDarkFast",32,0,0)
		DRKV EF 3 A_FaceTarget
		DRKV G 3 A_CustomMissile("DanmakuDarkFast",32,0,0)
		DRKV A 0 A_JumpIfInTargetInventory("EasyMode",1,"See")
		DRKV EF 3 A_FaceTarget
		DRKV G 3 A_CustomMissile("DanmakuDarkFast",32,0,0)
		DRKV A 0 A_JumpIfInTargetInventory("NormalMode",1,"See")
		DRKV EF 3 A_FaceTarget
		DRKV G 3 A_CustomMissile("DanmakuDarkFast",32,0,0)
		DRKV A 0 A_JumpIfInTargetInventory("HardMode",1,"See")
		DRKV EF 3 A_FaceTarget
		DRKV G 3 A_CustomMissile("DanmakuDarkFast",32,0,0)
		Goto See
	MissileEasy:
		DRKV A 0 A_Jump(128,"MissileRepeatFire")
		DRKV EF 6 A_FaceTarget
		DRKV G 0 A_CustomMissile("DanmakuDarkStrong",32,0,-30)
		DRKV G 0 A_CustomMissile("DanmakuDarkStrong",32,0,30)
		DRKV G 6 A_CustomMissile("DanmakuDarkStrong",32,0,0)
		Goto See
	MissileNormal:
		DRKV A 0 A_Jump(128,"MissileRepeatFire")
		DRKV EF 6 A_FaceTarget
		DRKV G 0 A_CustomMissile("DanmakuDarkStrong",32,0,-40)
		DRKV G 0 A_CustomMissile("DanmakuDarkStrong",32,0,40)
		DRKV G 0 A_CustomMissile("DanmakuDarkStrong",32,0,-20)
		DRKV G 0 A_CustomMissile("DanmakuDarkStrong",32,0,20)
		DRKV G 6 A_CustomMissile("DanmakuDarkStrong",32,0,0)
		Goto See
	MissileHard:
		DRKV A 0 A_Jump(128,"MissileRepeatFire")
		DRKV EF 6 A_FaceTarget
		DRKV G 0 A_CustomMissile("DanmakuDarkStrong",32,0,-45)
		DRKV G 0 A_CustomMissile("DanmakuDarkStrong",32,0,45)
		DRKV G 0 A_CustomMissile("DanmakuDarkStrong",32,0,-30)
		DRKV G 0 A_CustomMissile("DanmakuDarkStrong",32,0,30)
		DRKV G 0 A_CustomMissile("DanmakuDarkStrong",32,0,-15)
		DRKV G 0 A_CustomMissile("DanmakuDarkStrong",32,0,15)
		DRKV G 6 A_CustomMissile("DanmakuDarkStrong",32,0,0)
		Goto See
	MissileLunatic:
		DRKV A 0 A_Jump(128,"MissileRepeatFire")
		DRKV EF 6 A_FaceTarget
		DRKV G 0 A_CustomMissile("DanmakuDarkStrong",32,0,-48)
		DRKV G 0 A_CustomMissile("DanmakuDarkStrong",32,0,48)
		DRKV G 0 A_CustomMissile("DanmakuDarkStrong",32,0,-36)
		DRKV G 0 A_CustomMissile("DanmakuDarkStrong",32,0,36)
		DRKV G 0 A_CustomMissile("DanmakuDarkStrong",32,0,-24)
		DRKV G 0 A_CustomMissile("DanmakuDarkStrong",32,0,24)
		DRKV G 0 A_CustomMissile("DanmakuDarkStrong",32,0,-12)
		DRKV G 0 A_CustomMissile("DanmakuDarkStrong",32,0,12)
		DRKV G 6 A_CustomMissile("DanmakuDarkStrong",32,0,0)
		Goto See
    Pain:
        DRKV H 2
        DRKV H 2 A_Pain
        Goto See
    Death:
        DRKV L 8
        DRKV M 8 A_Scream
        DRKV N 5
        DRKV O 5 A_Fall
        DRKV P -1
        Stop
    XDeath:
        DRKI N 5
        DRKI O 5 A_XScream
        DRKI P 5
        DRKI Q 5 A_Fall
        DRKI RST 5
        DRKI U -1
        Stop
    Raise:
        DRKV PO 8
        DRKV NML 6
        Goto See
    }
}

actor DanmakuDarkFast
{
	DamageType "DanmakuImp"
    Radius 8
    Height 6
    Speed 18
    Damage 3
	+STRIFEDAMAGE
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    Decal DoomImpScorch
    States
    {
    Spawn:
        DKB1 AB 4 Bright
        Loop
    Death:
        DKB1 CDE 4 Bright
        Stop
    }
}

actor DanmakuDarkStrong
{
	DamageType "DanmakuImp"
    Radius 8
    Height 6
    Speed 9
    Damage 6
	+STRIFEDAMAGE
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    Decal DoomImpScorch
    States
    {
    Spawn:
        DKB2 AB 4 Bright A_SpawnItemEx("DarkTail", 0, 0, 0, 0, 0, 0, 0, 128)
        Loop
    Death:
        DKB2 CDE 4 Bright
        Stop
    }
}

actor DarkTail
{
    PROJECTILE
    RENDERSTYLE ADD
    +NOCLIP
    ALPHA 0.5
    States
    {
    Spawn:
        DKBT ABCDEF 2 Bright
        Stop
    }
}

